; Copyright (C) 2007-2012 Adam CÃ©cile for Mandriva
; http://www.mandriva.com/
;
; This file is part of Pulse 2, http://pulse2.mandriva.org
;
; Pulse 2 is free software; you can redistribute it and/or modify
; it under the terms of the GNU General Public License as published by
; the Free Software Foundation; either version 2 of the License, or
; (at your option) any later version.
;
; Pulse 2 is distributed in the hope that it will be useful,
; but WITHOUT ANY WARRANTY; without even the implied warranty of
; MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the
; GNU General Public License for more details.
;
; You should have received a copy of the GNU General Public License
; along with Pulse 2; if not, write to the Free Software
; Foundation, Inc., 51 Franklin Street, Fifth Floor, Boston,
; MA 02110-1301, USA.


; Basic defines
!define PRODUCT_NAME "Mandriva Pulse2 Win32 Agents"
!define PRODUCT_PUBLISHER "Mandriva S.A."
!define PRODUCT_WEB_SITE "http://www.mandriva.com"

; Agents defines
!define SSH_AGENT_EXE "@@ssh_file@@"
!define VNC_AGENT_EXE "@@vnc_file@@"
!define INVENTORY_AGENT_EXE "@@inv_file@@"

; Useful modules
!include "MUI.nsh"
!include "LogicLib.nsh"
!include "WinVer.nsh"
!include "Sections.nsh"
!include "FileFunc.nsh"
!include "libs\ServiceLib.nsh"
!include "libs\StrRep.nsh"
!include "libs\InstallOptionsMacro.nsh"

; Get command line parameters and values
!insertmacro GetParameters
!insertmacro GetOptions

; Variable to store from where the 7sfx archive has been run
Var /GLOBAL ARCHIVE_DIR

; No Prompt (NP) silent with graphics
Var /GLOBAL NO_PROMPT

; Address of the inventory server
Var /GLOBAL INV_SERVER
Var /GLOBAL INV_SERVER_PORT

; MUI settings
!define MUI_ABORTWARNING
!define MUI_WELCOMEPAGE_TITLE_3LINES
!define MUI_HEADERIMAGE
!define MUI_HEADERIMAGE_RIGHT
!define MUI_ICON "artwork\install.ico"
!define MUI_HEADERIMAGE_BITMAP "artwork\header.bmp"
!define MUI_WELCOMEFINISHPAGE_BITMAP "artwork\wizard.bmp"

; Installer pages
!define MUI_PAGE_CUSTOMFUNCTION_PRE pagePre ; A way to override page in No Prompt mode
!insertmacro MUI_PAGE_WELCOME
!define MUI_PAGE_CUSTOMFUNCTION_PRE pagePre ; A way to override page in No Prompt mode
!insertmacro MUI_PAGE_COMPONENTS
; Custom page to deal with inventory options
Page custom InventoryOptions
!define MUI_PAGE_CUSTOMFUNCTION_PRE lastPagePre ; Let's auto close the installer in No Prompt mode
!insertmacro MUI_PAGE_INSTFILES
!insertmacro MUI_LANGUAGE "English"

; Reserve file for options
ReserveFile "inventoryoptions.ini"
!insertmacro MUI_RESERVEFILE_INSTALLOPTIONS

Name "${PRODUCT_NAME}"
OutFile "pulse2-win32-agents-installer.exe"
ShowInstDetails show

Section "Secure Agent" SSH
  DetailPrint "Secure Agent Installation...."
  DetailPrint "------------------------------------------------------"
  StrCpy $0 "$EXEDIR\${SSH_AGENT_EXE} /S /FORCE"
  ${IfNot} ${FileExists} "$EXEDIR\id_dsa.pub"
    ${IfNot} ${FileExists} "$EXEDIR\id_rsa.pub"
      ${If} ${FileExists} "$ARCHIVE_DIR\id_dsa.pub"
        DetailPrint "Copying $ARCHIVE_DIR\id_dsa.pub to $EXEDIR\id_dsa.pub"
        CopyFiles "$ARCHIVE_DIR\id_dsa.pub" "$EXEDIR\id_dsa.pub"
      ${Else}
        ${If} ${FileExists} "$ARCHIVE_DIR\id_rsa.pub"
          DetailPrint "Copying $ARCHIVE_DIR\id_rsa.pub to $EXEDIR\id_rsa.pub"
          CopyFiles "$ARCHIVE_DIR\id_rsa.pub" "$EXEDIR\id_rsa.pub"
        ${Else}
          MessageBox MB_OK|MB_ICONEXCLAMATION "id_dsa.pub/id_rsa.pub not found ! $\nPlease copy it to current directory before installing."
          Abort
        ${EndIf}
      ${EndIf}
    ${EndIf}
  ${EndIf}
  DetailPrint "Running $0"
  nsExec::ExecToLog $0
  Pop $1 # return value/error/timeout
  DetailPrint "Return code was: $1"
  DetailPrint ""
  DetailPrint ""
SectionEnd

Section "Remote Desktop Agent" VNC
  DetailPrint "Remote Desktop Agent Installation...."
  DetailPrint "------------------------------------------------------"

  ; Clean old TightVNC 2.0.2 if installed
  ClearErrors
  ReadRegStr $0 HKLM "Software\Microsoft\Windows\CurrentVersion\Uninstall\TightVNC" "DisplayVersion"
  ${IfNot} ${Errors}
    ; Seems TightVNC 2.0.2 is installed
    ClearErrors
    ${IfNot} ${Errors}
      ReadRegStr $0 HKLM "Software\Microsoft\Windows\CurrentVersion\Uninstall\TightVNC" "UninstallString"
      DetailPrint "TightVNC 2.0.2 seems to be installed. Uninstalling..."
      ; Silent uninstall
      StrCpy $0 `"$0" /S`
      DetailPrint "Running $0"
      nsExec::ExecToLog $0
      Pop $1 # return value/error/timeout
      DetailPrint "Return code was: $1"
      DetailPrint ""
    ${Else}
      DetailPrint "Uh? Skipping TightVNC 2.0.2 uninstallation."
      DetailPrint "Got DisplayVersion from registry without UninstallString, that's weird."
      DetailPrint ""
    ${EndIf}
  ${EndIf}

  ;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;
  ; Create installation command line                                                          ;
  ; http://www.tightvnc.com/doc/win/TightVNC_2.7_for_Windows_Installing_from_MSI_Packages.pdf ;
  ;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;
  StrCpy $0 `msiexec /i "$EXEDIR\${VNC_AGENT_EXE}" /quiet /norestart`
  StrCpy $0 `$0 ADDLOCAL=Server SERVER_REGISTER_AS_SERVICE=1 SERVER_ADD_FIREWALL_EXCEPTION=0 SERVER_ALLOW_SAS=1`
  ; Disable embedded Java WebSrv on port 5800
  StrCpy $0 `$0 SET_ACCEPTHTTPCONNECTIONS=1 VALUE_OF_ACCEPTHTTPCONNECTIONS=0`
  ; Enable RFB on port 5900
  StrCpy $0 `$0 SET_ACCEPTRFBCONNECTIONS=1 VALUE_OF_ACCEPTRFBCONNECTIONS=1`
  ; Enable loopback connection
  StrCpy $0 `$0 SET_ALLOWLOOPBACK=1 VALUE_OF_ALLOWLOOPBACK=1`
  ; Loopback only
  StrCpy $0 `$0 SET_LOOPBACKONLY=1 VALUE_OF_LOOPBACKONLY=1`
  ; Only allow from 127.0.0.1 and query user
  StrCpy $0 `$0 SET_IPACCESSCONTROL=1 VALUE_OF_IPACCESSCONTROL=127.0.0.1:2`
  ; Default answser on timeout is reject
  StrCpy $0 `$0 SET_QUERYACCEPTONTIMEOUT=1 VALUE_OF_QUERYACCEPTONTIMEOUT=1`
  ; Timeout is 20s
  StrCpy $0 `$0 SET_QUERYTIMEOUT=1 VALUE_OF_QUERYTIMEOUT=20`
  ; Show service icon
  StrCpy $0 `$0 SET_RUNCONTROLINTERFACE=1 VALUE_OF_RUNCONTROLINTERFACE=1`
  ; Hide wallpaper
  StrCpy $0 `$0 SET_REMOVEWALLPAPER=1 VALUE_OF_REMOVEWALLPAPER=1`
  ; Share between multiple connection
  StrCpy $0 `$0 SET_ALWASHARED=1 SET_NEVERSHARED=1 VALUE_OF_ALWASHARED=1 VALUE_OF_NEVERSHARED=0`
  ; Disable authentication
  StrCpy $0 `$0 SET_USEVNCAUTHENTICATION=1 VALUE_OF_USEVNCAUTHENTICATION=0`
  ; Ensure remote inputs are enabled
  StrCpy $0 `$0 SET_BLOCKREMOTEINPUT=1 VALUE_OF_BLOCKREMOTEINPUT=0`
  ; Don't do anything when terminating VNC session
  StrCpy $0 `$0 SET_DISCONNECTACTION=1 VALUE_OF_DISCONNECTACTION=0`

  DetailPrint "Running $0"
  nsExec::ExecToLog $0
  Pop $1 # return value/error/timeout
  DetailPrint "Return code was: $1"

  ; \o/ New TightVNC seems to handle everything by itself
  ;
  ; StrCpy $1 "tvnserver"
  ; !insertmacro SERVICE "status" $1 ""
  ; Pop $0
  ; ${IfNot} $0 == "stopped"
  ;   DetailPrint "Stopping Remote Desktop Agent service"
  ;   !insertmacro SERVICE "stop" $1 ""
  ;   Sleep 1000
  ; ${EndIf}
  ; DetailPrint "Starting Remote Desktop Agent service"
  ; !insertmacro SERVICE "start" $1 ""
  ; Sleep 1000
  ; Run service helper (trayicon for current session)
  ; ReadRegStr $0 HKLM "SYSTEM\CurrentControlSet\Services\$1" "ImagePath"
  ; Remove "-service" from command line
  ; ${StrReplace} $2 "-service" " " $0
  ; New VNC systray icon doesn't background itself
  ; Let's help it with a bit of windows shell magic...
  ; StrCpy $1 `cmd.exe /C start "" $2 -controlservice -slave`
  ; DetailPrint "Starting Remote Desktop Agent service helper for current session"
  ; DetailPrint "Running $1"
  ; nsExec::ExecToLog $1

  DetailPrint ""
  DetailPrint ""
SectionEnd

Section "Inventory Agent" Inventory
  DetailPrint "Inventory Agent Installation...."
  DetailPrint "------------------------------------------------------"

  ${IfNot} ${Silent} 
  ${IfNot} $NO_PROMPT == "true"
    ReadINIStr $0 "$PLUGINSDIR\inventoryoptions.ini" "Field 3" "State"
    ${If} $0 == ""
      MessageBox MB_OK|MB_ICONEXCLAMATION "Pulse2 Inventory server IP address is empty! $\n\
Please fill the field with the right IP address."
      Abort
    ${EndIf}
    StrCpy $INV_SERVER $0
    ReadINIStr $1 "$PLUGINSDIR\inventoryoptions.ini" "Field 5" "State"
    ${If} $1 == ""
      MessageBox MB_OK|MB_ICONEXCLAMATION "Pulse2 Inventory server port is empty! $\n\
Please fill the field with the right port."
      Abort
    ${EndIf}
    StrCpy $INV_SERVER_PORT $1
  ${EndIF}
  ${EndIF}

  StrCpy $0 "$EXEDIR\${INVENTORY_AGENT_EXE} /S @@inv_tag@@ /installtasks=Minimal /acceptlicense /runnow /server=$\"http://$INV_SERVER:$INV_SERVER_PORT$\""
  DetailPrint "Running $0"
  nsExec::ExecToLog $0
  Pop $1 # return value/error/timeout
  DetailPrint "Return code was: $1"
  DetailPrint ""
  DetailPrint ""
SectionEnd

!insertmacro MUI_FUNCTION_DESCRIPTION_BEGIN
  !insertmacro MUI_DESCRIPTION_TEXT ${SSH} "Secure Agent"
  !insertmacro MUI_DESCRIPTION_TEXT ${VNC} "Remote Desktop Agent"
  !insertmacro MUI_DESCRIPTION_TEXT ${Inventory} "Inventory Agent"
!insertmacro MUI_FUNCTION_DESCRIPTION_END

Function .onInit
  ; NT4 or later
  ${IfNot} ${IsNT}
    MessageBox MB_OK|MB_ICONEXCLAMATION "You cannot install $(^Name) unless you're running Windows NT later."
    Quit
  ${EndIf}
  
  ${GetParameters} $R0
  
  ; Handle /A option
  ${GetOptions} $R0 "/A=" $0
  StrCpy $ARCHIVE_DIR $0
  
  ; Handle /S option
  ${GetOptions} $R0 /S $0
  ${If} ${Errors} ; "Silent mode" flag not set
    SetSilent normal
  ${Else} ; "Silent mode" flag set
    SetSilent silent
  ${EndIf}
  
  ; Handle /NP option
  ${GetOptions} $R0 /NP $0
  ${If} ${Errors} ; not set
    StrCpy $NO_PROMPT false
  ${Else}
    StrCpy $NO_PROMPT true
  ${EndIf}
  
  ; Handle /INV_SERVER option
  ${GetOptions} $R0 "/INV_SERVER=" $0
  StrCpy $INV_SERVER $0
  
  ; Handle /INV_SERVER_PORT option
  ${GetOptions} $R0 "/INV_SERVER_PORT=" $0
  StrCpy $INV_SERVER_PORT $0
  
  ; Required for custom pages
  !insertmacro MUI_INSTALLOPTIONS_EXTRACT "inventoryoptions.ini"
  ; Forbid ssh installation if the installer is missing
  ${IfNot} ${FileExists} ${SSH_AGENT_EXE}
    SectionSetFlags ${SSH} ${SF_RO}
  ${EndIf}
  ; Forbid vnc installation if the installer is missing
  ${IfNot} ${FileExists} ${VNC_AGENT_EXE}
    SectionSetFlags ${VNC} ${SF_RO}
  ${EndIf}
  ; Forbid inventory installation if the installer is missing
  ${IfNot} ${FileExists} ${INVENTORY_AGENT_EXE}
    SectionSetFlags ${Inventory} ${SF_RO}
  ${EndIf}
FunctionEnd

Function InventoryOptions
  ; Skip in NP (No Prompt) mode
  ${IfNot} $NO_PROMPT == "true"
    ${If} ${SectionIsSelected} ${Inventory}
      !insertmacro MUI_HEADER_TEXT "Mandriva Pulse2 inventory server IP address" "Configure Pulse2 inventory server IP address (and port if needed)."
      !insertmacro INSTALLOPTIONS_INITDIALOG "inventoryoptions.ini"
      ; Try to pre-fill fileds with command line parameters
      ${IfNot} $INV_SERVER == ""
        !insertmacro CHANGETEXTFIELD "inventoryoptions.ini" "Field 3" $INV_SERVER
      ${EndIf}
      ${IfNot} $INV_SERVER_PORT == ""
        !insertmacro CHANGETEXTFIELD "inventoryoptions.ini" "Field 5" $INV_SERVER_PORT
      ${EndIf}
      ; Don't show in NP (No Prompt) mode
      !insertmacro INSTALLOPTIONS_SHOW
    ${EndIf}
  ${EndIf}
FunctionEnd

Function pagePre
  ${If} $NO_PROMPT == "true"
    Abort
  ${EndIf}
FunctionEnd

Function lastPagePre
  ${If} $NO_PROMPT == "true"
    SetAutoClose true
  ${EndIf}
FunctionEnd
